SICP 読書ノート#35 - 3.5.1 ストリームは遅延リスト (pp.187-192)
======================================

いよいよストリームへ。

生まれて初めてその概念に触れたけど驚きの連続。特に```delay```と```force```による実装が何とも直截的で素敵すぎる。やはりSICPはもっと早くに読むべきだった…


## ストリーム入門

ここまで解釈系は[Racket](http://racket-lang.org/)を使っているのですが、そのRacketにはデフォルトでstreamsというパッケージがあるので、今回はそれを使います。

* http://docs.racket-lang.org/reference/streams.html

```scheme
(require racket/stream)
```

各種手続きをSICP風の表記に合わせて再定義。

```scheme
(define stream-null? stream-empty?)
(define stream-car stream-first)
(define stream-cdr stream-rest)
(define the-empty-stream empty-stream)
```

どうも```stream-cons```は特殊形式らしく```(define cons-stream stream-cons)```で再定義しようとするとエラーするので、```car-stream```ではなく```stream-car```、```cdr-stream```ではなく```stream-cdr```としました。

合わせて```display-stream```も定義。ただRacketには```stream->list```という便利な手続きがあるので、わざわざ定義しなくてもいいかもしれない。

```scheme
(define (display-stream s)
  (stream-for-each
   (lambda (x) (display (format "~a " x))) s)
  (newline))
```

テキスト通り```enumerate-interval```のストリーム版を写経。

```scheme
(define (stream-enumerate-interval low high)
  (if (> low high)
      the-empty-stream
      (stream-cons
       low
       (stream-enumerate-interval (+ low 1) high))))
```

テスト。当たり前だけどちゃんと動いてる。

```scheme
racket@> (define s (stream-enumerate-interval 10000 1000000))
racket@> (stream-car s)
10000
racket@> (stream-car (stream-cdr s))
10001

;; prime? を使うためにロード
racket@> (require math/number-theory)

racket@> (define primes (stream-filter prime? s))
racket@> (stream-car primes)
10007
racket@> (stream-car (stream-cdr primes))
10009
```

リスト操作で本当に遅延評価されているのか、速度を測ってみる。ストリームなし版の```enumerate-interval```は2章で出てきた通り。

```scheme
(define (enumerate-interval low high)
  (if (> low high)
      nil
      (cons low (enumerate-interval (+ low 1) high))))
```

テスト。

```scheme
racket@> (time (list-ref (enumerate-interval 10000 10000000) 10000))
cpu time: 9206 real time: 10190 gc time: 6286
20000

racket@> (time (stream-ref (stream-enumerate-interval 10000 10000000) 10000))
cpu time: 6 real time: 8 gc time: 0
20000
```

改めて書くまでもないけど、ストリームなし版は```enumerate-interval```は先に先頭から終端までのリストを展開してからでないと```list-ref```を評価できないため時間がかかる。ストリーム版は```stream-enumerate-interval```が即座に```(stream-cons 10000 (delay (stream-enumerate-interval 10001 10000000)))```という対を返し、````stream-ref```が```(force (delay ...))```で評価して次の要素へ進むというのの繰り返しになるので、参照したい要素まで分の計算しか走らない。よって速度に大きな差が生じる。


## 練習問題

理解したつもりになったところで練習問題へ。


### 問題 3.50

以下の```stream-map```を完成させよ。穴埋めなのでかなり助かる。

```scheme
(define (stream-map proc . argstreams)
  (if (<??> (car argstreams))
	  the-empty-stream
	  (<??>
	   (apply proc (map <??> argstreams))
	   (apply stream-map
			  (cons proc (map <??> argstreams))))))
```

まず2章に戻って```map```の実装を写経する。

```scheme
(define (mono-map proc items)
  (if (null? items)
      nil
      (cons (proc (car items))
            (mono-map proc (cdr items)))))
```

2章では脚注でちょろっと登場しただけだったが、これの複数リストが扱える版を実装する。

```scheme
(define (high-map proc . argitems)
  (if (null? (car argitems))
	  nil
	  (cons
	   (apply proc (mono-map car argitems))
	   (apply high-map
			  (cons proc (mono-map cdr argitems))))))
```

テスト。ちゃんと動いている。

```scheme
racket@> (mono-map (lambda (n) (+ 1 n)) (list 1 2 3))
'(2 3 4)
racket@> (high-map (lambda (n) (+ 1 n)) (list 1 2 3))
'(2 3 4)
racket@> (high-map + (list 1 2 3) (list 4 5 6))
'(5 7 9)
```

これまでの内容だと```stream-map```はこの```high-map```の中をstream手続きに置き換えればよいだけなので、次のようになるはず。

```scheme
(define (high-stream-map proc . argstreams)
  (if (stream-null? (car argstreams))
	  the-empty-stream
	  (stream-cons
	   (apply proc (high-map stream-car argstreams))
	   (apply high-stream-map
			  (cons proc (high-map stream-cdr argstreams))))))
```

また、検証用に```list->stream```を実装。

```scheme
(define (list->stream sequence)
  (if (null? sequence)
	  nil
	  (stream-cons (car sequence)
				   (list->stream (cdr sequence)))))
```

道具はそろったのでテスト。

```scheme
racket@> (stream->list
		  (high-stream-map +
						   (list->stream (list 1 2 3))
						   (list->stream (list 4 5 6))))
=> '(5 7 9)
```

おお！

ちなみにracket/streamsの```stream-map```は複数リストを扱えないので、後でこの```high-stream-map```が活躍すると思います。


### 問題 3.51

```scheme
(define (display-line x)
  (display x)
  (newline))

(define (show x)
  (display-line x)
  x)

(define x
  (stream-map show
			  (stream-enumerate-interval 0 10)))
```

とした時に```(stream-ref x 5)```および```(stream-ref x 7)```の結果はどうなるか？

予想では、

```scheme
> (stream-ref x 5)
0
1
2
3
4
5
=> 5

> (stream-ref x 7)
0
1
2
3
4
5
6
7
=> 7
```

なんだけど、racket/streamsは最適化されているのか、余計な評価は行わない。

```scheme
racket@> (stream-ref x 5)
5
=> 5

racket@> (stream-ref x 7)
7
=> 7
```

さらに続けて```(stream->list x)```を実行してみると、１度評価された結果はメモされているので```5```、```7```はプリントされない。

```scheme
racket@> (stream->list x)
0
1
2
3
4
6
8
9
10
=> '(0 1 2 3 4 5 6 7 8 9 10)
```


### 問題 3.52

```scheme
(define sum 0)

(define (accum x)
  (set! sum (+ x sum))
  sum)

(define seq
  (stream-map accum
			  (stream-enumerate-interval 1 20)))

(define y (stream-filter even? seq))

(define z
  (stream-filter (lambda (x) (= (remainder x 5) 0))
				 seq))

```

において、

```scheme
(stream-ref y 7)
(display-stream z)
```

の実行結果はどうなるか？

まず```seq```は```(stream-enumerate-interval 1 20)```の並びに対して、その最初からその項までの和を項とする並びになる。つまり、```(stream-enumerate-interval 1 20)```を\\( \\lbrace i\_{n} \\rbrace = \\lbrace 1, 2, 3, ..., 20 \\rbrace \\) と定義した場合、```seq```の各項は、

* \\( s\_{0} = i\_{0} \\)
* \\( s\_{1} = i\_{1} + i\_{0} \\)
* \\( s\_{2} = i\_{2} + i\_{1} + i\_{0} \\)
* ...
* \\( s\_{k} = i\_{k} + \\sum_{j = 0}^{k - 1} i\_{j} \\)

となるため \\( \\lbrace s_{n} \\rbrace = \\lbrace 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, \\dots, 210 \\rbrace \\) のような並びになる。

よって、```(stream-ref y 7)``` の結果は \\( \\lbrace s\_{n} \\rbrace \\) の7番目の偶数となるので```136```、```(display-stream z)```は \\( \\lbrace s\_{n} \\rbrace \\) の5の倍数を順次プリントするので```10 15 45 55 105 120 190 210```が表示されるはず。

テスト。

```scheme
racket@> (stream-ref y 7)
136
racket@> (display-stream z)
10 15 45 55 105 120 190 210
```

合ってる。

メモ化が行われない```force```を使うケースでは、```seq```を走査する度に```sum```に値が蓄積されてしまうので結果がおかしくなってしまう模様。


次は「§3.5.2 無限ストリーム」から。


--------------------------------

※「SICP読書ノート」の目次は[こちら](/entry/sicp/index)

<!---
<script
  type="text/javascript"
  src="https://c328740.ssl.cf1.rackcdn.com/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML"
  ></script>
--->

<script type="text/x-mathjax-config">
  MathJax.Hub.Config({ tex2jax: { inlineMath: [['$','$'], ["\\(","\\)"]] } });
</script>
<script type="text/javascript"
  src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_HTML">
</script>
<meta http-equiv="X-UA-Compatible" CONTENT="IE=EmulateIE7" />
