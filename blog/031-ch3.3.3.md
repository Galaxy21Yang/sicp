SICP 読書ノート#30 - 3.3.3 表の表現 (pp.xx-yy)
======================================

§3.3.3 「表の表現」から。

### 問題 3.24

解答は省略。

```make-table``` にテスト関数を引数で渡せるようにし、
```assoc-tree``` 内の ```equal?``` の代わりに
そのテスト関数を使えばよいだけ。

### 問題 3.25

テキストの例がわかりにくすぎるので、
record、treeはコンストラクタを定義して分割した。

2章の「抽象の壁」以降さんざんやったのに、
3章のこのあたりの節はその辺のアプローチが雑な気がする。


まずはecord。key、valueの対である。

```scheme
(define (make-record k v)
  (cons k v))
(define (key record)
  (car record))
(define (value record)
  (cdr record))
(define (set-key! record k)
  (set-car! record k))
(define (set-value! record v)
  (set-cdr! record v))
```

次にtree。recordの単方向リストである。

```scheme
(define (make-tree record next)
  (cons record next))
(define (record tree)
  (car tree))
(define (next tree)
  (cdr tree))
(define (set-record! tree record)
  (set-car! tree record))
(define (set-next! tree next)
  (set-cdr! tree next))
```

ここからユーティリティ関数。

asscoc-tree。treeからkeyに一致するrecordを返す。

```scheme
(define (assoc-tree k tree)
  (cond ((null? tree)
		 false)
        ((equal? k (key (record tree)))
		 (record tree))
        (else (assoc-tree k (next tree)))))
```

make-deep-record。key-listとvalueを引数に取り、
keyの数分だけの深さを持つrecordを返す。

```scheme
(define (make-deep-record key-list v)
  (if (null? (cdr key-list))
	  (make-record (car key-list) v)
	  (make-record (car key-list)
				   (make-tree (make-deep-record (cdr key-list) v)
							  nil))))
```

adjoin-record!。records、key-list、valueを引数に取り。
key-list、valueで生成されるrecordをrecordsに追加する。

```scheme
(define (adjoin-record! records key-list v)
  (set-value! records
			  (make-tree (make-deep-record key-list v)
						 (value records))))
```

テーブルの実装。

- テーブル本体は ```the-hash``` という変数
- メソッドとして ```lookup```、```insert```、```print``` を用意

```scheme
(define (make-table)
  (let ((the-hash (make-record '*table* nil)))
	(define (lookup key-list)
	  (define (iter key-list records)
		(if (null? key-list)
			false
			(let ((record (assoc-tree (car key-list) (value records))))
			  (if record
				  (if (null? (cdr key-list))
					  (value record)
					  (iter (cdr key-list) record))
				  false))))
	  (iter key-list the-hash))
	(define (insert! key-list v)
	  (define (iter key-list records)
		(if (null? key-list)
			false
			(let ((record (assoc-tree (car key-list) (value records))))
			  (if record
				  (if (null? (cdr key-list))
					  (set-value! record v)
					  (iter (cdr key-list) record))
				  (adjoin-record! records key-list v)))))
	  (iter key-list the-hash))
	(define (print)
	  (begin
		(display the-hash (current-error-port))
		(newline (current-error-port))))

	(define (dispatch m)
	  (cond ((eq? m 'lookup-proc) lookup)
			((eq? m 'insert-proc!) insert!)
			((eq? m 'print-proc) print)
			(else (error "Unknown operation -- TABLE" m))))
	dispatch))


(define (lookup-table table key)
  ((table 'lookup-proc) key))
(define (insert-table! table key value)
  ((table 'insert-proc!) key value))
(define (print-table table)
  ((table 'print-proc)))
```

テスト。

```scheme
racket@> (insert-table! tbl (list 'foo 'bar) 1)
racket@> (insert-table! tbl (list 'foo 'baz) 2)
racket@> (insert-table! tbl (list 'foo 'qux) 3)
racket@> (insert-table! tbl (list 'bar 'baz) 11)
racket@> (insert-table! tbl (list 'bar 'qux) 12)
racket@> (print-table tbl)
(*table* (bar (qux . 12) (baz . 11)) (foo (qux . 3) (baz . 2) (bar . 1)))

racket@> (lookup-table tbl (list 'foo 'baz))
2
racket@> (lookup-table tbl (list 'foo 'foo))
#f
racket@> (lookup-table tbl (list 'bar 'foo))
#f
racket@> (lookup-table tbl (list 'bar 'baz))
11
```


### 問題 3.26



### 問題 3.27




次は「§3.3.4 ディジタル回路のシミュレータ」から。

--------------------------------

※「SICP読書ノート」の目次は[こちら](/entry/sicp/index)



