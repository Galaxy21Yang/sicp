SICP 読書ノート#36 - 3.5.2 無限ストリーム #1 (pp.193-196)
======================================

**“To inifity and beyond”**

トイ・ストーリーでのBuzz Lightyearの決め台詞です。

無限ストリーム(infinite streams)を極めると、さらにその先の世界が見えるかもしれない :)

[asin:B00FF4I1JS:detail]


## 無限ストリームとは

無限に1が続く```ones```というストリームは以下のように定義できる。

```scheme
(define ones
  (stream-cons 1 ones))
```

至ってシンプル。今の僕ではこんなの逆立ちしても思いつかない…

実際に操作してみる。```stream->list```は永遠に評価し続けるので返ってこない。

```scheme
racket@> (time (stream-ref ones 0))
cpu time: 0 real time: 0 gc time: 0
1
racket@> (time (stream-ref ones 1))
cpu time: 0 real time: 0 gc time: 0
1
racket@> (time (stream-ref ones 10000))
cpu time: 3 real time: 3 gc time: 0
1
racket@> (time (stream-ref ones 1000000))
cpu time: 211 real time: 210 gc time: 0
1
racket@> (stream->list ones)
=> 返ってこない
```

実際にどう評価されているかを頭で追ってみると、

```scheme
(stream-ref ones 100)
=> (stream-ref (stream-cons 1 ones) 100)
=> (stream-ref (stream-cdr ones) 99)
=> (stream-ref ones 99)
=> (stream-ref (stream-cdr ones) 98)
=> (stream-ref ones 98)
...
=> (stream-ref ones 1)
=> (stream-ref (stream-cdr ones) 0)
=> (stream-ref ones 0)
=> (stream-car ones)
=> 1
```

よくよくみるとこれって、

* \\( o\_{0} = 1 \\)
* \\( o\_{1} = o\_{0} \\)
* \\( o\_{2} = o\_{1} \\)
* ...
* \\( o\_{k} = o\_{k - 1} \\)

の要素を持つ数列 \\( \\{o\_{n}\\} = \\{1, 2, \\dots, o\_{k}, \\dots \\} \\) と同じである。要は```(define ones ...)```というのは、初項 \\( 1 \\) で一般項 \\( o\_{k} = o\_{k - 1} \\) を持つ数列と等価であり、この数列の表現をSchemeの並びとして実装したとも言える。

さらに、 \\( \\{o\_{n}\\} \\) の \\( k \\) 番目からの部分列を \\( \\{o\_{n(k)}\\} \\) のように表記すると、

\\( \\{o\_{n}\\} = \\{1, \\{o\_{n(1)}\\}\\} = \\{1, \\{1, \\{o\_{n(2)}\\}\\}\\} = \\dots = \\{1, \\{1, \\dots \\{1, \\{o\_{n(k)}\\}\\}\\}\\} \\)

と定義できる。なので、```(stream-ref <stream> k)```は```<stream>```の部分列を```k```回分剥がした結果を取り出しているとも言える。


次に```intergers```について考える。

```scheme
(define integers
  (stream-cons 1 (add-streams ones integers)))
```

これも数列として捉えると、

* \\( i\_{0} = 1 \\)
* \\( i\_{1} = o\_{0} + i_\{0} = 2 \\)
* \\( i\_{2} = o\_{1} + i_\{1} = 3 \\)
* ...
* \\( i\_{k} = o\_{k - 1} + i_\{k - 1} = k + 1 \\)

となるので、

\\(
  \\{i\_{n}\\}
  = \\{1, \\{ o\_{n(0)} + i\_{n(0)} \\} \\}
  = \\{1, \\{ o\_{0} + i\_{0}, \\{ o\_{n(1)} + i\_{n(1)} \\} \\} \\}
  = \\{1, \\{ o\_{0} + i\_{0}, \\dots \\{ o\_{k - 1} + i\_{k - 1}, \\{ o\_{n(k)} + i\_{n(k)} \\} \\} \\} \\}
\\)

と定義できる。

逆に数列として明示的に定義されれば、無限ストリームで実装可能である。

例えばフィボナッチ数の場合、

* \\( f\_{0} = 0 \\)
* \\( f\_{1} = 1 \\)
* \\( f\_{2} = f\_{1} + f\_{0} \\)
* ...
* \\( f\_{k} = f\_{k - 1} + f\_{k - 2} \\)

なので、

```scheme
(define fib
  (stream-cons 0
			   (stream-cons 1
							(add-streams
							 fibs
							 (stream-cdr fibs)))))
```
										
と定義通りに実装すればよい。

無限ストリームを生成する手続きを実装することもできる。

```scheme
(define (integers-starting-from n)
  (stream-cons n (integers-starting-from (+ n 1))))
```

これを使って素数の無限ストリームを生成する。

```scheme
(define primes
  (stream-cons
   2
   (stream-filter prime? (integers-starting-from 3))))
```
   
テスト。

```scheme
racket@> (map (lambda (i) (stream-ref primes i))
			  (enumerate-interval 0 10))
=> '(2 3 5 7 11 13 17 19 23 29 31)
```

おもしろい。


## 練習問題

### 問題 3.53

以下のストリームの要素について説明せよ。

```scheme
(define s (stream-cons 1 (add-streams s s)))
```

数列の定義から考える。

* \\( a\_{0} = 1 \\)
* \\( a\_{1} = a\_{0} + a\_{0} = 2 \\)
* \\( a\_{2} = a\_{1} + a\_{1} = 4 \\)
* \\( a\_{3} = a\_{2} + a\_{2} = 8 \\)
* ...
* \\( a\_{k} = a\_{k - 1} + a\_{k - 1} = 2\^k \\)

テスト。定義通り。

```scheme
racket@> (map (lambda (i) (stream-ref s i))
			  (enumerate-interval 0 10))
=> '(1 2 4 8 16 32 64 128 256 512 1024)
```

### 問題 3.54

```mul-stream```は素直に実装すればよい。

```scheme
(define (mul-stream s1 s2)
  (high-stream-map * s1 s2))
```

階乗の数列は以下のように定義される。

* \\( f\_{0} = 1 \\)
* \\( f\_{1} = 1 * f\_{0} \\)
* \\( f\_{2} = 2 * f\_{1} \\)
* \\( f\_{3} = 3 * f\_{2} \\)
* ...
* \\( f\_{k} = k * f\_{k - 1} \\)

定義通りに実装すればよいので、

```scheme
(define factorials
  (stream-cons 1 (mul-streams integers factorials)))
```

テスト。

```scheme
racket@> (map (lambda (i) (stream-ref factorials i))
			  (enumerate-interval 0 10))
=> '(1 1 2 6 24 120 720 5040 40320 362880 3628800)
```

### 問題 3.55

```practical-sum```の各項は以下の通りになるので、

* \\( p\_{0} = s_\{0} \\)
* \\( p\_{1} = s_\{0} + s\_{1} = p\_{0} + s_\{1} \\)
* \\( p\_{2} = s_\{0} + s\_{1} + s\_{2} = p\_{1} + s_\{1} \\)
* ...
* \\( p\_{k} = s_\{0} + \\dots + s\_{k - 1} = p\_{k - 1} + s_\{k - 1} \\)

これもまた定義通りに実装する。

```scheme
(define (practical-sum s)
  (stream-cons (stream-car s)
			   (add-streams (practical-sum s) (stream-cdr s))))
```

テスト。

```scheme
racket@> (map (lambda (i) (stream-ref (practical-sum integers) i))
			  (enumerate-interval 0 10))
=> '(1 3 6 10 15 21 28 36 45 55 66)
```

OK。だいぶつかめてきた。


### 問題 3.56

1と2、3、5の倍数の並びを作る。

```merge```を組み合わせることに気づくのにすごい時間がかかったorz

```scheme
(define S
  (stream-cons 1
			   (merge (scale-stream S 2)
					  (merge (scale-stream S 3) (scale-stream S 5)))))
```

テスト。

```scheme
racket@> (map (lambda (i) (stream-ref S i))
			  (enumerate-interval 0 20))
=> '(1 2 3 4 5 6 8 9 10 12 15 16 18 20 24 25 27 30 32 36 40)
```

### 問題 3.57

どこでやらなかったっけ？既視感がすごいあるが。

メモ化されている場合は、n回の加算で済む。

メモ化されていない場合は、

* ```fib_{n} + fib_{n-1}```を求めるには、```fib_{n-1} + fib_{n-2}```と```fib_{n-1} + fib_{n-2}```の加算が必要で、
* ```fib_{n-1} + fib_{n-2}```を求めるには、```fib_{n-2} + fib_{n-3}```と```fib_{n-3} + fib_{n-4}```の加算が必要で、
* ...

といった具合で指数的に増加する。


### 問題 3.58

まずは脳内で解いてみる。

```scheme
    (expand 3 4 10)
=> '(7 (expand 2 5 10))
=> '(7 (5 (expand 0 5 10)))
=> '(7 (5 (0 (expand 0 5 10))))
=> '(7 (5 (0 (0 (expand 0 5 10)))))
=> '(7 (5 (0 (0 (0 ... (expand 0 5 10))))))
```

となるので、```radix```を基数とした除算結果をストリームで返す手続き。

テスト。```expand```という名前はRacketの組み込みマクロと重複するようなので、別の名前で実装。

```scheme
(define (my-expand num den radix)
  (stream-cons
   (quotient (* num radix) den)
   (my-expand (remainder (* num radix) den) den radix)))

;; test
racket@> (map (lambda (i) (stream-ref (my-expand 3 4 10) i))
			  (enumerate-interval 0 10))
=> '(7 5 0 0 0 0 0 0 0 0 0)

racket@> (map (lambda (i) (stream-ref (my-expand 4 3 10) i))
			  (enumerate-interval 0 10))
=> '(13 3 3 3 3 3 3 3 3 3 3)
```


ソースコードの全文はこちらにあります。

* https://github.com/uents/sicp/blob/master/ch3.5.2.scm

次回は「§3.5.2 無限ストリーム」の練習問題の続きから。


--------------------------------

※「SICP読書ノート」の目次は[こちら](/entry/sicp/index)


<script type="text/x-mathjax-config">
  MathJax.Hub.Config({ tex2jax: { inlineMath: [['$','$'], ["\\(","\\)"]] } });
</script>
<script type="text/javascript"
  src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_HTML">
</script>
<meta http-equiv="X-UA-Compatible" CONTENT="IE=EmulateIE7" />
