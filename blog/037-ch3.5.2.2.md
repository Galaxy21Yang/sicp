SICP 読書ノート#37 - 3.5.2 無限ストリーム #2 (pp.196-197)
======================================

### 問題 3.59

べき級数、聞き覚えはあるから習ったことはあるんだろうけど、さっぱり思い出せない。こんな時はWikipedia。先生いつもありがとう。

- [http://ja.wikipedia.org/wiki/%E5%86%AA%E7%B4%9A%E6%95%B0:title]


#### (a)

べき級数の項のストリームを引数に取り、それを積分した結果の項のストリームを返す```integrate-series```を実装する。

引数のストリームを \\( \\{s\_{n}\\} \\) とすると、

* \\( i\_{0} = s\_{0} \\)
* \\( i\_{1} = \\frac{1}{2} s\_{1} \\)
* \\( i\_{2} = \\frac{1}{3} s\_{2} \\)
* \\( i\_{3} = \\frac{1}{4} s\_{3} \\)
* ...
* \\( i\_{k} = \\frac{1}{k} s\_{k} \\)

のようなストリームを返す手続きを実装すればよいので、

```scheme
(define (integrate-series s)
  (define (iter s k)
	(cons-stream (/ (stream-car s) k)
				 (iter (stream-cdr s) (+ k 1))))
  (iter s 1))
```

テスト。

```scheme
racket@> (define i (integrate-series ones))
racket@> (map (lambda (x) (stream-ref i x))
			  (enumerate-interval 0 5))
=> '(1 1/2 1/3 1/4 1/5 1/6)
```

#### (b)

まずは```exp-series```を動かしてみる。

```scheme
(define exp-series
  (cons-stream 1 (integrate-series exp-series)))

;; test
racket@> (map (lambda (x) (stream-ref exp-series x))
			  (enumerate-interval 0 5))
=> '(1 1 1/2 1/6 1/24 1/120)
```

確かに、

\\( e\^{x} = 1 + x + \\frac{x\^2}{2!} + \\frac{x\^3}{3!} + \\dots \\)

の定義通り \\( k \\) 番目の項が \\( \\frac{1}{k!} \\) となっている。うーん、すごい。

次にcos、sinの級数の式について、

\\( \\cos x = 1 - \\frac{x\^2}{2!} + \\frac{x\^4}{4!} - \\dots \\)

\\( \\sin x = x - \\frac{x\^3}{3!} + \\frac{x\^5}{5!} - \\dots \\)

それぞれ積分してみると、

\\( \\int \\cos x = x - \\frac{x\^3}{3!} + \\frac{5\^4}{5!} - \\dots = \\sin x \\)

\\( \\int \\sin x = \\frac{x\^2}{2!} - \\frac{x\^4}{4!} + \\frac{x\^6}{6!} - \\dots = 1 - \\cos x \\)

より、

\\( \\cos x = 1 - \\int \\sin x \\)

\\( \\sin x = \\int \\cos x \\)

となるので、この定義をそのまま実装する。

```scheme
(define cosine-series
  (cons-stream 1 (scale-stream (integrate-series sine-series) -1)))

(define sine-series
  (cons-stream 0 (integrate-series cosine-series)))
```

テスト。元々のべき級数の式と同じになる。

```scheme
racket@> (map (lambda (i) (stream-ref cosine-series i))
			  (enumerate-interval 0 10))
=> '(1 0 -1/2 0 1/24 0 -1/720 0 1/40320 0 -1/3628800)

racket@> (map (lambda (i) (stream-ref sine-series i))
			  (enumerate-interval 0 10))
=> '(0 1 0 -1/6 0 1/120 0 -1/5040 0 1/362880 0)
```


### 問題 3.60

2つのべき級数の項のストリームをそれぞれ \\( \\{ a\_{n} \\} \\) 、\\( \\{ b\_{n} \\} \\) とすると、2つのべき級数の積の項のストリームは、

* \\( m\_{0} = a\_{0} * b\_{0} \\)
* \\( m\_{1} = a\_{0} * b\_{1} + a\_{1} * b\_{0} \\)
* \\( m\_{2} = a\_{0} * b\_{2} + a\_{1} * b\_{1} + a\_{2} * b\_{0} \\)
* \\( m\_{3} = a\_{0} * b\_{3} + a\_{1} * b\_{2} + a\_{2} * b\_{1} + a\_{3} * b\_{0} \\)
* ...

となる。

問題の穴埋めについて

```scheme
(define (mul-series s1 s2)
  (cons-stream ⟨??⟩ (add-streams ⟨??⟩ ⟨??⟩)))
```

\\( m\_{0} \\) より最初の```<??>```は自明。

```scheme
(* (stream-car s1) (stream-car s2))
```

2、3番目の```<??>```は```add-streams```の引数となるので、

```scheme
(add-streams <??> (mul-series <??> <??>))
```

のように畳み込みながら足すイメージなんだけど、全然思いつかず…

1〜2日寝かせて、図を凝視していたら分かった。

* \\( m\_{0} = a\_{0} * b\_{0} \\)
* \\( m\_{1} = a\_{0} * b\_{1} + a\_{1} * b\_{0} \\)
* \\( m\_{2} = a\_{0} * b\_{2} + a\_{1} * b\_{1} + a\_{2} * b\_{0} \\)
* \\( m\_{3} = a\_{0} * b\_{3} + a\_{1} * b\_{2} + a\_{2} * b\_{1} + a\_{3} * b\_{0} \\)
* ...
* \\( m = a\_{0} * \\{ b\_{n} \\} + a\_{1} * \\{ b\_{n} \\} + a\_{2} * \\{ b\_{n} \\} + a\_{3} * \\{ b\_{n} \\} + \\dots \\)


上の図の通りに実装。

```scheme
(define (mul-series s1 s2)
  (cons-stream (* (stream-car s1) (stream-car s2))
			   (add-streams (scale-stream (stream-cdr s2) (stream-car s1))
							(mul-series (stream-cdr s1) s2))))
```

テスト。

```scheme
racket@> (map (lambda (i) (stream-ref (mul-series integers integers) i))
			  (enumerate-interval 0 5))
=> '(1 4 10 20 35 56)
```

ｷﾀ━(ﾟ∀ﾟ)━!


### 問題 3.61-62

問題3.60と同様、ほとんど数学なのでパス。


次回は「§3.5.3 ストリームパラダイムの開発」から。

--------------------------------

※「SICP読書ノート」の目次は[こちら](/entry/sicp/index)


<script type="text/x-mathjax-config">
  MathJax.Hub.Config({ tex2jax: { inlineMath: [['$','$'], ["\\(","\\)"]] } });
</script>
<script type="text/javascript"
  src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_HTML">
</script>
<meta http-equiv="X-UA-Compatible" CONTENT="IE=EmulateIE7" />
