SICP 読書ノート#64 - 4.4 論理型プログラミング (pp.261-269)
======================================

いよいよ4章の最後のセクション「論理型プログラミング」に入りました。

ここでのキーワードは「一方向性計算から多方向性計算へ」と「ユニフィケーション」のようです。


また、本文の`append`の定義について

```scheme
(define (append x y)
  (if (null? x)
      y
      (cons (car x) (append (cdr x) y))))
```

に対し、

> この手続きは, 次の二つの規則をLispに翻訳したものと考えられる. 第一の規則は, 第一のリストが空の場合を扱い, 第二は二つの部分のconsである, 空ではないリストの場合を扱う:
> 
> • 任意のリストyについて, 空リストとyをappendするとyになる.
> 
> • 任意のu, v, yとzについて, vとyをappendしてzになるなら, (cons u v)とyをappendすると, (cons u z)になる

を公理的定義というようですが、後から出てくるのでひとまず置いておきます。


### 質問システムを動かす

まずは質問システム(query system)を動かしてみます。

#### (1) 質問システムのソースコードを取得

SICP公式サイトから取得。

```sh
% curl -O https://mitpress.mit.edu/sicp/code/ch4-query.scm
```

#### (2) 質問システムを一部修正

以下のように`ch4-query.scm`を修正します。
 
- loadに成功するよう`instantiate`と`query-driver-loop`の定義順を入れ替え
- `racket/stream`がそのまま使えるよう`cons-stream`→`stream-cons`
- `eval`の引数を`(interaction-environment)`へ

```diff
--- ch4-query.orig.scm	1996-09-05 03:17:22.000000000 +0900
+++ ch4-query.scm	2015-08-11 13:52:45.000000000 +0900
@@ -19,6 +19,18 @@
 (define input-prompt ";;; Query input:")
 (define output-prompt ";;; Query results:")
 
+(define (instantiate exp frame unbound-var-handler)
+  (define (copy exp)
+    (cond ((var? exp)
+           (let ((binding (binding-in-frame exp frame)))
+             (if binding
+                 (copy (binding-value binding))
+                 (unbound-var-handler exp frame))))
+          ((pair? exp)
+           (cons (copy (car exp)) (copy (cdr exp))))
+          (else exp)))
+  (copy exp))
+
 (define (query-driver-loop)
   (prompt-for-input input-prompt)
   (let ((q (query-syntax-process (read))))
@@ -41,17 +53,6 @@
              (qeval q (singleton-stream '()))))
            (query-driver-loop)))))
 
-(define (instantiate exp frame unbound-var-handler)
-  (define (copy exp)
-    (cond ((var? exp)
-           (let ((binding (binding-in-frame exp frame)))
-             (if binding
-                 (copy (binding-value binding))
-                 (unbound-var-handler exp frame))))
-          ((pair? exp)
-           (cons (copy (car exp)) (copy (cdr exp))))
-          (else exp)))
-  (copy exp))
 
 
 ;;;SECTION 4.4.4.2
@@ -124,7 +125,7 @@
 ;;(put 'lisp-value 'qeval lisp-value)
 
 (define (execute exp)
-  (apply (eval (predicate exp) user-initial-environment)
+  (apply (eval (predicate exp) (interaction-environment))
          (args exp)))
 
 (define (always-true ignore frame-stream) frame-stream)
@@ -279,13 +280,13 @@
   (store-assertion-in-index assertion)
   (let ((old-assertions THE-ASSERTIONS))
     (set! THE-ASSERTIONS
-          (cons-stream assertion old-assertions))
+          (stream-cons assertion old-assertions))
     'ok))
 
 (define (add-rule! rule)
   (store-rule-in-index rule)
   (let ((old-rules THE-RULES))
-    (set! THE-RULES (cons-stream rule old-rules))
+    (set! THE-RULES (stream-cons rule old-rules))
     'ok))
 
 (define (store-assertion-in-index assertion)
@@ -295,7 +296,7 @@
                (get-stream key 'assertion-stream)))
           (put key
                'assertion-stream
-               (cons-stream assertion
+               (stream-cons assertion
                             current-assertion-stream))))))
 
 (define (store-rule-in-index rule)
@@ -306,7 +307,7 @@
                  (get-stream key 'rule-stream)))
             (put key
                  'rule-stream
-                 (cons-stream rule
+                 (stream-cons rule
                               current-rule-stream)))))))
 
 (define (indexable? pat)
@@ -326,14 +327,14 @@
 (define (stream-append-delayed s1 delayed-s2)
   (if (stream-null? s1)
       (force delayed-s2)
-      (cons-stream
+      (stream-cons
        (stream-car s1)
        (stream-append-delayed (stream-cdr s1) delayed-s2))))
 
 (define (interleave-delayed s1 delayed-s2)
   (if (stream-null? s1)
       (force delayed-s2)
-      (cons-stream
+      (stream-cons
        (stream-car s1)
        (interleave-delayed (force delayed-s2)
                            (delay (stream-cdr s1))))))
@@ -350,7 +351,7 @@
 
 
 (define (singleton-stream x)
-  (cons-stream x the-empty-stream))
+  (stream-cons x the-empty-stream))
 
 
 ;;;SECTION 4.4.4.7
@@ -473,7 +474,7 @@
 (define (stream-map proc s)
   (if (stream-null? s)
       the-empty-stream
-      (cons-stream (proc (stream-car s))
+      (stream-cons (proc (stream-car s))
                    (stream-map proc (stream-cdr s)))))
 
 (define (stream-for-each proc s)
@@ -491,7 +492,7 @@
 (define (stream-filter pred stream)
   (cond ((stream-null? stream) the-empty-stream)
         ((pred (stream-car stream))
-         (cons-stream (stream-car stream)
+         (stream-cons (stream-car stream)
                       (stream-filter pred
                                      (stream-cdr stream))))
         (else (stream-filter pred (stream-cdr stream)))))
@@ -499,13 +500,13 @@
 (define (stream-append s1 s2)
   (if (stream-null? s1)
       s2
-      (cons-stream (stream-car s1)
+      (stream-cons (stream-car s1)
                    (stream-append (stream-cdr s1) s2))))
 
 (define (interleave s1 s2)
   (if (stream-null? s1)
       s2
-      (cons-stream (stream-car s1)
+      (stream-cons (stream-car s1)
                    (interleave s2 (stream-cdr s1)))))
```
					

#### (3) ストリームの定義ファイルを用意

§3.5で作成した`streams.scm`をそのまま転用します。

```scheme
(define nil '())

;;; streamsを使うためにロード
(require racket/stream)

;;; SICPの表記に合わせて再定義
(define stream-null? stream-empty?)
;;(define cons-stream stream-cons) ;; 特殊形式のためcons-streamに再定義できない
(define stream-car stream-first)
(define stream-cdr stream-rest)
(define the-empty-stream empty-stream)
```

#### (4) Racket REPLから起動

Racket REPLから以下のように入力してくと起動。

マイクロシャフト社のデータベースは`ch4-query.scm`に含まれているので初期化してあげるだけです。

```scheme
racket@> (require r5rs)
racket@> (load "../streams.scm")
racket@> (load "ch4-query.scm")
racket@> (initialize-data-base microshaft-data-base)
'done
racket@> (query-driver-loop)

;;; Query input:

(あとはqueryを入力していく...)
```

これらを`start.scm`のようにひとつにまとめて`(load "start.scm")`としてももちろんよいです。

ここまでのソースコードは以下に置いています。

- https://github.com/uents/sicp/tree/master/ch4-query-system


### 単純質問

テキストの例を写経。

- プログラマを全て見つける

```scheme
;;; Query input:
(job ?who (computer programmer))

;;; Query results:
(job (Fect Cy D) (computer programmer))
(job (Hacker Alyssa P) (computer programmer))
```

- 全ての社員の住所をリストアップ

```scheme
;;; Query input:
(address ?x ?y)

;;; Query results:
(address (Aull DeWitt) (Slumerville (Onion Square) 5))
(address (Cratchet Robert) (Allston (N Harvard Street) 16))
(address (Scrooge Eben) (Weston (Shady Lane) 10))
(address (Warbucks Oliver) (Swellesley (Top Heap Road)))
(address (Reasoner Louis) (Slumerville (Pine Tree Road) 80))
(address (Tweakit Lem E) (Boston (Bay State Road) 22))
(address (Fect Cy D) (Cambridge (Ames Street) 3))
(address (Hacker Alyssa P) (Cambridge (Mass Ave) 78))
(address (Bitdiddle Ben) (Slumerville (Ridge Road) 10))
```

- コンピュータ部門の社員を見つける

```scheme
;;; Query input:
(job ?who (computer ?type))

;;; Query results:
(job (Tweakit Lem E) (computer technician))
(job (Fect Cy D) (computer programmer))
(job (Hacker Alyssa P) (computer programmer))
(job (Bitdiddle Ben) (computer wizard))
```

- さらに`.`をつけると複数のシンボルにもマッチする

```scheme
;;; Query input:
(job ?who (computer . ?type))

;;; Query results:
(job (Reasoner Louis) (computer programmer trainee))
(job (Tweakit Lem E) (computer technician))
(job (Fect Cy D) (computer programmer))
(job (Hacker Alyssa P) (computer programmer))
(job (Bitdiddle Ben) (computer wizard))
```

### 問題 4.55

#### a. Ben Bitdiddleに監督されている人すべて

```scheme
;;; Query input:
(supervisor ?who (Bitdiddle Ben))

;;; Query results:
(supervisor (Tweakit Lem E) (Bitdiddle Ben))
(supervisor (Fect Cy D) (Bitdiddle Ben))
(supervisor (Hacker Alyssa P) (Bitdiddle Ben))
```

#### b. 経理部門 [accounting division] のすべての人の名前と担当

```scheme
;;; Query input:
(job ?who (accounting . ?type))

;;; Query results:
(job (Cratchet Robert) (accounting scrivener))
(job (Scrooge Eben) (accounting chief accountant))
```

#### c. Slumerville に住む人すべての名前と住所

```scheme
;;; Query input:
(address ?who (Slumerville . ?where))

;;; Query results:
(address (Aull DeWitt) (Slumerville (Onion Square) 5))
(address (Reasoner Louis) (Slumerville (Pine Tree Road) 80))
(address (Bitdiddle Ben) (Slumerville (Ridge Road) 10))
```

### 合成質問

- 計算機プログラマのすべての住所を見つける

```scheme
;;; Query input:
(and (job ?person (computer programmer))
	 (address ?person ?where))

;;; Query results:
(and (job (Fect Cy D) (computer programmer)) (address (Fect Cy D) (Cambridge (Ames Street) 3)))
(and (job (Hacker Alyssa P) (computer programmer)) (address (Hacker Alyssa P) (Cambridge (Mass Ave) 78)))
```

- Ben Bitdiddle か Alyssa P. Hacker が監督するすべての従業員

```scheme
;;; Query input:
(or (supervisor ?who (Bitdiddle Ben))
	(supervisor ?who (Hacker Alyssa P)))

;;; Query results:
(or (supervisor (Tweakit Lem E) (Bitdiddle Ben)) (supervisor (Tweakit Lem E) (Hacker Alyssa P)))
(or (supervisor (Reasoner Louis) (Bitdiddle Ben)) (supervisor (Reasoner Louis) (Hacker Alyssa P)))
(or (supervisor (Fect Cy D) (Bitdiddle Ben)) (supervisor (Fect Cy D) (Hacker Alyssa P)))
(or (supervisor (Hacker Alyssa P) (Bitdiddle Ben)) (supervisor (Hacker Alyssa P) (Hacker Alyssa P)))
```

- Ben Bitdiddleが監督し、計算機プログラマでない人すべて

```scheme
;;; Query input:
(and (supervisor ?who (Bitdiddle Ben))
	 (not (job ?who (computer programmer))))

;;; Query results:
(and (supervisor (Tweakit Lem E) (Bitdiddle Ben)) (not (job (Tweakit Lem E) (computer programmer))))
```

- 給料が30,000ドルより多い人すべて

```scheme
;;; Query input:
(and (salary ?person ?amount)
	 (lisp-value > ?amount 30000))

;;; Query results:
(and (salary (Scrooge Eben) 75000) (lisp-value > 75000 30000))
(and (salary (Warbucks Oliver) 150000) (lisp-value > 150000 30000))
(and (salary (Fect Cy D) 35000) (lisp-value > 35000 30000))
(and (salary (Hacker Alyssa P) 40000) (lisp-value > 40000 30000))
(and (salary (Bitdiddle Ben) 60000) (lisp-value > 60000 30000))
```

### 問題 4.56

#### a. Ben Bitdiddleが監督している人すべての名前とその住所

```scheme
;;; Query input:
(and (supervisor ?person (Bitdiddle Ben))
	 (address ?person ?address))

;;; Query results:
(and (supervisor (Tweakit Lem E) (Bitdiddle Ben)) (address (Tweakit Lem E) (Boston (Bay State Road) 22)))
(and (supervisor (Fect Cy D) (Bitdiddle Ben)) (address (Fect Cy D) (Cambridge (Ames Street) 3)))
(and (supervisor (Hacker Alyssa P) (Bitdiddle Ben)) (address (Hacker Alyssa P) (Cambridge (Mass Ave) 78)))
```

#### b. 給料がBen Bitdiddleのそれより少ない人のすべてと、その人たちの給料と、Ben Bitdiddleの給料

```scheme
;;; Query input:
(and (salary (Bitdiddle Ben) ?ben-amount)
	 (and (salary ?person ?amount)
		  (lisp-value < ?amount ?ben-amount)))

;;; Query results:
(and (salary (Bitdiddle Ben) 60000) (and (salary (Aull DeWitt) 25000) (lisp-value < 25000 60000)))
(and (salary (Bitdiddle Ben) 60000) (and (salary (Cratchet Robert) 18000) (lisp-value < 18000 60000)))
(and (salary (Bitdiddle Ben) 60000) (and (salary (Reasoner Louis) 30000) (lisp-value < 30000 60000)))
(and (salary (Bitdiddle Ben) 60000) (and (salary (Tweakit Lem E) 25000) (lisp-value < 25000 60000)))
(and (salary (Bitdiddle Ben) 60000) (and (salary (Fect Cy D) 35000) (lisp-value < 35000 60000)))
(and (salary (Bitdiddle Ben) 60000) (and (salary (Hacker Alyssa P) 40000) (lisp-value < 40000 60000)))
```

#### c. 計算機部門にいない人が監督している人すべてと、その監督者の名前と担当

```scheme
;;; Query input:
(and (supervisor ?staff-person ?boss)
	 (not (job ?boss (computer . ?type)))
	 (job ?boss ?job))

;;; Query results:
(and (supervisor (Aull DeWitt) (Warbucks Oliver)) (not (job (Warbucks Oliver) (computer . ?type))) (job (Warbucks Oliver) (administration big wheel)))
(and (supervisor (Cratchet Robert) (Scrooge Eben)) (not (job (Scrooge Eben) (computer . ?type))) (job (Scrooge Eben) (accounting chief accountant)))
(and (supervisor (Scrooge Eben) (Warbucks Oliver)) (not (job (Warbucks Oliver) (computer . ?type))) (job (Warbucks Oliver) (administration big wheel)))
(and (supervisor (Bitdiddle Ben) (Warbucks Oliver)) (not (job (Warbucks Oliver) (computer . ?type))) (job (Warbucks Oliver) (administration big wheel)))
```

見づらいのでまとめると、

| 計算機部門にいない人が監督している人 | 監督者の名前 | 監督者の担当 |
|--------------------------------------|--------------|--------------|
| Aull DeWitt      | Warbucks Oliver | administration big wheel      |
| Cratchet Robert  | Scrooge Even    | accounting chief accountant   |
| Scrooge Eben     | Warbucks Oliver | 同上                          |
| Bitdiddle Ben    | Warbucks Oliver | 同上                          |


### 規則

規則(rule)は質問(query)そのものを抽象化する手段を提供します。

- 同じ町に住む人を見つける規則 `lives-near`

```scheme
(rule (lives-near ?person-1 ?person-2)
      (and (address ?person-1 (?town . ?rest-1))
           (address ?person-2 (?town . ?rest-2))
           (not (same ?person-1 ?person-2))))
```

- 同じかどうかをチェックする規則 `same`

```scheme
(rule (same ?x ?x))
```

- 監督者を監督する人を見つける `wheel`

```scheme
(rule (wheel ?person)
      (and (supervisor ?middle-manager ?person)
           (supervisor ?x ?middle-manager)))
```

- スタッフとボスの関係を表す `outranked-by`。再帰的に表現することもできる

```scheme
(rule (outranked-by ?staff-person ?boss)
      (or (supervisor ?staff-person ?boss)
          (and (supervisor ?staff-person ?middle-manager)
               (outranked-by ?middle-manager ?boss))))
```


これらの規則を使って質問を投げかけてみる。


- 近くに住んでいる規則の規定と、Ben Bitdiddleの近くに住む人は誰か？

```scheme
;;; Query input:
(lives-near ?who (Bitdiddle Ben))

;;; Query results:
(lives-near (Aull DeWitt) (Bitdiddle Ben))
(lives-near (Reasoner Louis) (Bitdiddle Ben))
```

- Benの近くに住む計算機プログラマは？

```scheme
;;; Query input:
(and (job ?x (computer . ?type))
	 (lives-near ?x (Bitdiddle Ben)))

;;; Query results:
(and (job (Reasoner Louis) (computer programmer trainee)) (lives-near (Reasoner Louis) (Bitdiddle Ben)))
```

### 問題 4.57

マイクロシャフト社のデータベース `microshaft-data-base` に以下の規則を追加する。

```scheme
(rule (replace ?person-1 ?person-2)
	  (and (job ?person-1 ?job-1)
		   (job ?person-2 ?job-2)
		   (or (same ?job-1 ?job-2)
			   (can-do-job ?job-1 ?job-2))
		   (not (same ?person-1 ?person-2))))
```

#### a. Cy D. Fectに代われる人すべて

```scheme
;;; Query input:
(replace ?who (Fect Cy D))

;;; Query results:
(replace (Bitdiddle Ben) (Fect Cy D))
(replace (Hacker Alyssa P) (Fect Cy D))
```

#### b. 誰かに代われて、その誰かのほうが多くの給料をもらっている人全てと両者の給料

```scheme
;;; Query input:
(and (replace ?person-1 ?person-2)
	 (salary ?person-1 ?salary-1)
	 (salary ?person-2 ?salary-2)
	 (lisp-value > ?salary-1 ?salary-2))

;;; Query results:
(and (replace (Fect Cy D) (Reasoner Louis)) (salary (Fect Cy D) 35000) (salary (Reasoner Louis) 30000) (lisp-value > 35000 30000))
(and (replace (Hacker Alyssa P) (Reasoner Louis)) (salary (Hacker Alyssa P) 40000) (salary (Reasoner Louis) 30000) (lisp-value > 40000 30000))
(and (replace (Bitdiddle Ben) (Tweakit Lem E)) (salary (Bitdiddle Ben) 60000) (salary (Tweakit Lem E) 25000) (lisp-value > 60000 25000))
(and (replace (Bitdiddle Ben) (Fect Cy D)) (salary (Bitdiddle Ben) 60000) (salary (Fect Cy D) 35000) (lisp-value > 60000 35000))
(and (replace (Bitdiddle Ben) (Hacker Alyssa P)) (salary (Bitdiddle Ben) 60000) (salary (Hacker Alyssa P) 40000) (lisp-value > 60000 40000))
(and (replace (Hacker Alyssa P) (Fect Cy D)) (salary (Hacker Alyssa P) 40000) (salary (Fect Cy D) 35000) (lisp-value > 40000 35000))
```

### 問題 4.58

`microshaft-data-base`に以下の規則を追加する。

```scheme
(rule (big-shot ?person)
	  (and (supervisor ?person ?boss)
		   (not (replace ?boss ?person))))
```

テスト。

```scheme
;;; Query input:
(big-shot ?person)

;;; Query results:
(big-shot (Aull DeWitt))
(big-shot (Cratchet Robert))
(big-shot (Scrooge Eben))
(big-shot (Bitdiddle Ben))
```

### 問題 4.59

`microshaft-data-base`に以下を追加する。

```scheme
(meeting accounting (Monday 9am))
(meeting administration (Monday 10am))
(meeting computer (Wednesday 3pm))
(meeting administration (Friday 1pm))
(meeting whole-company (Wednesday 4pm))
```

#### a. 金曜にあるすべてのミーティング

```scheme
;;; Query input:
(meeting ?section (Friday ?time))

;;; Query results:
(meeting administration (Friday 1pm))
```

#### b. `(rule (metting-time ?person ?day-and-time) <rule-body>)` を実装

```scheme
(rule (meeting-time ?person ?day-and-time)
	  (or (and (job ?person (?section . ?type))
			   (meeting ?section ?day-and-time))
		  (meeting whole-company ?day-and-time)))
```

#### c. Alyssaが出席すべき水曜のミーティング

```scheme
;;; Query input:
(meeting-time (Hacker Alyssa P) (Wednesday ?time))

;;; Query results:
(meeting-time (Hacker Alyssa P) (Wednesday 3pm))
(meeting-time (Hacker Alyssa P) (Wednesday 4pm))
```

### 問題 4.60

- 理由は、規則`lives-near`が重複チェックを`(not (same ?person-1 ?person-2))`でしかしていないため
- 重複チェックとしてさらに名前の比較チェックを追加すればよい。実装はパス


次回は「プログラムとしての論理」から。

--------------------------------

※「SICP読書ノート」の目次は[こちら](/entry/sicp/index)


<script type="text/x-mathjax-config">
  MathJax.Hub.Config({ tex2jax: { inlineMath: [['$','$'], ["\\(","\\)"]] } });
</script>
<script type="text/javascript"
  src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_HTML">
</script>
<meta http-equiv="X-UA-Compatible" CONTENT="IE=EmulateIE7" />
